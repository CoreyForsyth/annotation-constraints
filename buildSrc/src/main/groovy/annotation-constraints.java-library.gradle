plugins {
    id('annotation-constraints.java')
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

jar {
    from('../LICENSE') {
        into("META-INF")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url.set( "https://github.com/CoreyForsyth/annotation-constraints" )
                developers {
                    developer {
                        name.set("Corey Forsyth")
                        organizationUrl.set("https://github.com/CoreyForsyth")
                    }
                }
                scm {
                    connection.set( "scm:git:git://github.com/CoreyForsyth/annotation-constraints.git" )
                    url.set( "https://github.com/CoreyForsyth" )
                }
                issueManagement {
                    system.set( "GitHub" )
                    url.set( "https://github.com/CoreyForsyth/issues" )
                }
                licenses {
                    license {
                        name.set('MIT License')
                        url.set('http://www.opensource.org/licenses/mit-license.php')
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'OSSRH'
            def snapshot = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            def staging = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            url = version.endsWith('SNAPSHOT') ? snapshot : staging
            credentials {
                username = project.hasProperty('ossrhUsername') ? project.getProperty('ossrhUsername') : ""
                password = project.hasProperty('ossrhPassword') ? project.getProperty('ossrhPassword') : ""
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}